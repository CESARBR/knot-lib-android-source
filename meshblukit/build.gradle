apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"
    resourcePrefix "meshblukit__"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile('com.github.nkzawa:engine.io-client:0.5.0'){
        exclude group: 'org.json', module: 'json'
    }
    compile 'com.mcxiaoke.volley:library:1.0.18'
    compile 'com.octoblu:sanejsonobject:4.0.1'
    compile 'com.android.support:support-annotations:23.0.0'
}

def siteUrl = 'https://github.com/octoblu/MeshbluKit-Android'      // Homepage URL of the library
def gitUrl = 'https://github.com/octoblu/MeshbluKit-Android.git'   // Git repository URL
group = "com.octoblu"                                                  // Maven Group ID for the artifact
version = "1.0.0"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "MeshbluKit"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'A SDK for using Meshblu'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/octoblu/MeshbluKit-Android/blob/master/LICENSE'
                    }
                }
                developers {
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}